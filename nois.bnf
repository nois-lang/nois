// TODO: boolean
// TODO: list constructor
// TODO: match expression
module              ::= statement*
                    ;
statement           ::= var-def | fn-def | type-def | return-stmt | expr
                    ;
  var-def           ::= LET-KEYWORD IDENTIFIER type-annot? EQUALS expr
                    ;
  fn-def            ::= FN-KEYWORD IDENTIFIER type-params? O-PAREN params? C-PAREN type-annot? block?
                    ;
  type-def          ::= TYPE-KEYWORD type-expr (constr-params | constr-list)
                    ;
    constr-params   ::= O-PAREN params? C-PAREN
                    ;
    constr-list     ::= O-BRACE (constructor (COMMA constructor)* COMMA?)? C-BRACE
                    ;
      constructor   ::= IDENTIFIER constr-params?
                    ;
  return-stmt       ::= RETURN-KEYWORD expr?
                    ;
  expr              ::= sub-expr (infix-op sub-expr)*
                    ;
    sub-expr        ::= prefix-op operand | operand postfix-op?
                    ;
      operand       ::= if-expr
                    | lambda-expr
                    | O-PAREN expr C-PAREN
                    | STRING
                    | CHAR
                    | NUMBER
                    | IDENTIFIER
                    | type-expr
                    ;
    infix-op        ::= add-op | sub-op | mul-op | div-op | exp-op | mod-op | access-op | eq-op | ne-op
                    | ge-op | le-op | gt-op | lt-op | and-op | or-op;
      add-op        ::= PLUS;
      sub-op        ::= MINUS;
      mul-op        ::= ASTERISK;
      div-op        ::= SLASH;
      exp-op        ::= CARET;
      mod-op        ::= PERCENT;
      access-op     ::= PERIOD;
      eq-op         ::= EQUALS EQUALS;
      ne-op         ::= EXCL EQUALS;
      ge-op         ::= C-ANGLE EQUALS;
      le-op         ::= O-ANGLE EQUALS;
      gt-op         ::= C-ANGLE;
      lt-op         ::= O-ANGLE;
      and-op        ::= AMPERSAND AMPERSAND;
      or-op         ::= PIPE PIPE;

    prefix-op       ::= add-op | sub-op | not-op | spread-op
                    ;
      not-op        ::= EXCL
                    ;
      spread-op     ::= PERIOD PERIOD
                    ;

    postfix-op      ::= call-op
                    ;
      call-op       ::= args
                    ;
        args        ::= O-PAREN (expr (COMMA expr)*)? COMMA? C-PAREN
                    ;
    lambda-expr     ::= lambda-params type-annot? (block | expr)
                    ;
      lambda-params ::= PIPE (param (COMMA param)*)? COMMA? PIPE
                    ;
    params          ::= O-PAREN (param (COMMA param)*)? COMMA? C-PAREN
                    ;
        param       ::= IDENTIFIER type-annot?
                    ;
block               ::= O-BRACE (statement (SEMI statement)* SEMI?)? C-BRACE
                    ;
type-annot          ::= COLON type-expr
                    ;
  type-expr         ::= IDENTIFIER type-params?
                    ;
    type-params     ::= O-ANGLE (type-expr (COMMA type-expr)* COMMA?)? C-ANGLE
                    ;
if-expr             ::= IF-KEYWORD expr block (ELSE-KEYWORD block)?
                    ;
hole                ::= UNDERSCORE
                    ;

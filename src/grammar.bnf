program         ::= statements
                ;

statements      ::= statement statements
                | statement
                ;

statement       ::= variable-def | type-def | return-stmt | expr
                ;

variable-def    ::= LET-KEYWORD IDENTIFIER EQUALS expr
                ;

type-def        ::= TYPE-KEYWORD IDENTIFIER TODO
                ;

return-stmt     ::= RETURN-KEYWORD expr
                | RETURN-KEYWORD
                ;

block           ::= OPEN-BRACE statements CLOSE-BRACE
                | OPEN-BRACE CLOSE-BRACE
                ;

expr            ::= sub-expr_ infix-operator sub-expr_
                | sub-expr_
                ;

sub-expr_       ::= prefix-op operand
                | operand postfix-op
                | operand
                ;

operand         ::= STRING
                | CHAR
                | NUMBER
                | IDENTIFIER
                | OPEN-PAREN expr CLOSE-PAREN
                | type
                | function-expr
                | if-expr
                ;

infix-operator  ::= PLUS | MINUS | ASTERISK | SLASH | CARET | PERCENT | PERIOD | EQUALS-OP | NOT-EQUALS-OP
                | CLOSE-CHEVRON | GREATER-EQ | OPEN-CHEVRON | LESS-EQ | AND | OR
                ;

prefix-op       ::= PLUS | MINUS | EXCL | SPREAD
                ;

postfix-op      ::= OPEN-PAREN args CLOSE-PAREN
                ;

args            ::= IDENTIFIER COMMA args
                | IDENTIFIER trailing-comma
                | IDENTIFIER
                | E
                ;

function-expr   ::= OPEN-PAREN params CLOSE-PAREN COLON type block
                ;

params          ::= param COMMA params
                | param trailing-comma
                | param
                | E
                ;

param           ::= IDENTIFIER COLON type
                ;

trailing-comma  ::= COMMA
                | E
                ;

type            ::= IDENTIFIER OPEN-CHEVRON type-params CLOSE-CHEVRON
                | IDENTIFIER
                ;

type-params     ::= type COMMA type-params
                | type trailing-comma
                | type
                | E
                ;

if-expr         ::= IF-KEYWORD expr block
                | IF-KEYWORD expr block ELSE-KEYWORD block
                ;
